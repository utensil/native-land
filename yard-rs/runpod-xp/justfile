set fallback
set dotenv-load

apikey := env_var("RUNPOD_API_KEY")

default:
    just --list

prep:
    uv init --no-readme --no-pin-python --python 3.11
    uv add -r requirements-runpod.txt --python 3.11 # --script *.py

deps:
    uv pip compile requirements-runpod.txt

sync-deps:
    uv sync

monit:
    uv run monit.py

kill:
    uv run kill.py

test-kill ID:
    RUNPOD_POD_ID={{ID}} python kill.py

run CONFIG="rust.yml":
    uv run run.py {{ justfile_directory() / "tasks" / CONFIG }}

gpus:
    #!/usr/bin/env bash
    curl --request POST \
        --header 'content-type: application/json' \
        --url 'https://api.runpod.io/graphql?api_key={{ apikey }}' \
        --data '{"query": "query GpuTypes { gpuTypes { id displayName memoryInGb } }"}'| jq | tee gpus.json |jq ".data.gpuTypes[].id"


