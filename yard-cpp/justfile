set fallback

example_xp := justfile_directory() / "example-xp"
# export CC := `which clang`
# export CXX := `which clang++`
export CC := '/opt/homebrew/Cellar/gcc@13/13.3.0/bin/gcc-13'
export CXX := '/opt/homebrew/Cellar/gcc@13/13.3.0/bin/g++-13'
export LD := '/opt/homebrew/Cellar/gcc@13/13.3.0/bin/g++-13'

default:
    just --list

prep-xrepo:
    curl -fsSL https://xmake.io/shget.text | bash

new NAME:
    xmake create -l c++ -P {{NAME}}

[group('ex')]
@prep-ex URL DIR BRANCH:
    #!/usr/bin/env bash
    mkdir -p {{example_xp}}
    cd {{example_xp}}
    if [[ ! -d {{example_xp / DIR}} ]]; then
        git clone --depth=100 --recurse-submodules {{URL}} {{example_xp / DIR}}
    fi
    cd {{example_xp / DIR}}
    git fetch --depth=100 origin {{BRANCH}}
    git checkout {{BRANCH}}

try-gabm: (prep-ex "https://github.com/utensil/ga-benchmark" "ga-benchmark" "master")
    cd {{example_xp / "ga-benchmark"}}

[no-cd]
build:
    xmake f --toolchain=gcc --cxx={{CXX}} --cc={{CC}} --ld={{LD}}
    xmake -vv
 
[no-cd]
run: build
    xmake run
