export HOMEBREW_NO_AUTO_UPDATE := "1"

default:
  just --list

# prep:
#   brew install zig

prep-asdf:
  which asdf || brew install asdf
  asdf plugin add zig https://github.com/zigcc/asdf-zig.git
  asdf plugin update zig

[no-cd]
prep-zig VERSION:
  asdf install zig {{VERSION}}
  asdf set zig {{VERSION}}
  # now zig is available either by `asdf which zig` or adding $HOME/.asdf/shims to your PATH

# REPO looks like git+https://HOST/USER/REPO.git
[no-cd]
fetch:
  #!/usr/bin/env zsh
  # working around https://github.com/ziglang/zig/issues/22542
  unset HTTP_PROXY
  unset HTTPS_PROXY
  unset http_proxy
  unset https_proxy
  zig build --fetch

[no-cd]
dep REPO:
  #!/usr/bin/env zsh
  # working around https://github.com/ziglang/zig/issues/22542
  unset HTTP_PROXY
  unset HTTPS_PROXY
  unset http_proxy
  unset https_proxy
  zig fetch --save {{REPO}}

[no-cd]
build: fetch

[no-cd]
run: fetch
  zig build run

[no-cd]
test: fetch
  zig build test

new PROJ:
  #!/usr/bin/env zsh
  set -e
  mkdir {{PROJ}}
  cd {{PROJ}}
  echo "Which version of zig do you want to use for project {{PROJ}}?"
  read -r ZIG_VERSION
  just prep-zig ${ZIG_VERSION}
  zig init

try-mlx INPUT MODEL_TYPE="qwen" NUM_TOKENS="1000":
  #!/usr/bin/env zsh
  cd example-xp
  if [ ! -d "MLX.zig" ]; then
    git clone https://github.com/jaco-bro/MLX.zig.git
  else
    (cd MLX.zig && git pull)
  fi
  cd MLX.zig
  just prep-zig 0.13.0
  zig build
  ./zig-out/bin/llm --num-tokens={{NUM_TOKENS}} --model-type={{MODEL_TYPE}} "{{INPUT}}"


